//KC03ABCA JOB ,'ALEX BERTOLASI',MSGCLASS=H
//JSTEP01  EXEC PGM=ASSIST
//STEPLIB  DD DSN=KC00NIU.ASSIST.LOADLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSIN    DD *
******************************************************************
* CSCI360-2                 PROJECT1                   FALL 2022 *
*                                                                *
*NAME: ALEX BERTOLASI                                            *
*DATE: 10/17/22                                                  *
*                                                                *
******************************************************************
*
* CUSTOMER DATA INPUT BUFFER
*
******************************************************************
$IBUFFR  DSECT                INPUT BUFFER FORMAT
$ICSTID  DS    CL8            CUSTOMER ID
         DS    C
$ICUSTNM DS    CL20           CUSTOMER NAME
         DS    C
$ISHPADR DS    CL30           SHIPPING ADDRESS
         DS    C
$IRGSLTX DS    CL4            REGIONAL SALES TAX
*
******************************************************************
*
* CUSTOMER DATA TABLE DSECT
*
******************************************************************
*
$CUSTINF DSECT                CUSTOMER TABLE FORMAT
$CUSTID  DS    F              CUSTOMER ID
         DS    CL2            (ENFORCE FULLWORD FOR RGSLSTX)
$CUSTNM  DS    CL20           CUSTOMER NAME
$SHPADDR DS    CL30           SHIPPING ADRESS
$RGSLSTX DS    F              REGIONAL SALES TAX
*
******************************************************************
*
* ORDER DATA INPUT BUFFER
*
******************************************************************
*
$ORDIN   DSECT                ORDER INFORMATION
$ICUST   DS    CL8            CUSTOMER ID
         DS    C
$IORDID  DS    CL8            ORDER ID
         DS    C
$IMONTH  DS    CL2            MONTH OF ORDER
         DS    C
$IDAY    DS    CL2            DAY OF ORDER
         DS    C
$ISUBTOT DS    CL6            SUBTOTAL OF ORDER
         DS    C
$ISHIPP  DS    CL4            SHIPPING OF ORDER
         DS    C
$IPAY    DS    CL10           PAYMENT METHOD
         DS    C
$ISTAT   DS    CL6            STATUS OF ORDER
         DS    C
*
******************************************************************
*
* ORDER DATA TABLE DSECT
*
******************************************************************
*
$ORDTBL  DSECT                ORDER TBALE FORMAT
$CSTID   DS    F              CUSTOMER ID
$ORDID   DS    F              ORDER ID
$MONTH   DS    F              MONTH OF ORDER
$DAY     DS    F              DAY OF ORDER
$SUBTOTL DS    F              SUBTOTAL OF ORDER
$SHPPNG  DS    F              SHPPING INFO
$PAYMETH DS    CL10           PAYMENT METHOD
$STATUS  DS    CL6            STATUS OF ORDER
*
******************************************************************
*
* FISCAL PREDICTION DATA INPUT BUFFER
*
******************************************************************
*
$IFSC    DSECT                FISCAL INPUT
$IMNTH   DS    CL2            MONTH
         DS    C
$IPREDCT DS    CL9            PREDICTED AMOUNT
         DS    C
$ILOWER  DS    CL9            LOWER BOUND
         DS    C
$IUPPER  DS    CL9            UPPER BOUND
         DS    C
*
******************************************************************
*
* FISCAL PREDICTION DATA TABLE
*
******************************************************************
*
$FSCTBL  DSECT                FISCAL TABLE FORMAT
$MNTH    DS    F              MONTH
$PREDCT  DS    F              PREDICTED FISCAL AMOUNT
$LOWER   DS    F              LOWER BOUND
$UPPER   DS    F              UPPER BOUND
*
******************************************************************
*
* OUTPUT BUFFER DSECT
*
******************************************************************
*
*$OBUFFER DSECT               OUTPUT BUFFER FORMAT
*         DS    CL4           LEFT MARGAIN
*$0CUSTID DS    F             CUSTOMER ID
*$OCUSTNM DS    CL20          CUSTOMER NAME
*$OSHPADR DS    CL30          SHIPPING ADDRESS
*$ORGSLTX DS    F             REGIONAL SALES TAX
*
******************************************************************
*
* START OF MAIN
*
******************************************************************
*
MAIN     CSECT
*
******************************************************************
*
* ENTRY LINKAGE
*
******************************************************************
*
*      Back up caller's register state
         STM   14,12,12(13)   SAVE REGS IN CALLER'S SAVE AREA
*      Establish local addressability
         LR    12,15          COPY CSECT ADDRESS INTO R12
         USING MAIN,12        ESTABLISH R12 AS THE BASE REG
*      Store backwards, forwards pointers
         LA    14,MAINSAVE    R14 POINTS TO THIS CSECT'S SAVE AREA
         ST    13,4(,14)      STORE ADDRESS OF CALLER'S SAVE AREA
         ST    14,8(,13)      STORE ADDRESS OF THIS CSECT'S SAVE AREA
*      Preemptively point r13 at local save area
         LR    13,14          POINT R13 AT THIS CSECT'S SAVE AREA
*
******************************************************************
*
* USING STATEMENTS FOR READING TABLES
*
******************************************************************
*
         USING $IBUFFR,2      USING DSECT CUSTOMER INPUT BUFFER -> R2
         USING $CUSTINF,3     USING DSECT CUSTOMER TABLE -> R3
         USING $ORDIN,6       USING DSECT ORDER INPUT -> R6
         USING $ORDTBL,7      USING DSECT ORDER TABLE -> R7
         USING $IFSC,9        USING DSECT FISCAL INPUT -> R9
         USING $FSCTBL,10     USING DSECT FISCAL TABLE -> R10
*
******************************************************************
*
* READ PROCESS FOR CUSTOMER DATA TABLE BEGINS
*
******************************************************************
*
         LA    2,IBUFFER      LOAD IBUFFER INTO R2
         LA    3,CUSTTBL      LOAD CUSTOMER TABLE INTO R3
         XREAD IBUFFER,80     PRIMING READ
*
******************************************************************
*
* READ LOOP FOR CUSTOMER DATA TABLE BEGINS
*
******************************************************************
*
CUSREAD  DS    0H
         CLI   IBUFFER,C'*'   COMPARE IBUFFER WITH IMMEDIATE CHAR '*'
         BE    ENDCUS         BRANCH OUT IF INPUT REACH '*' CHAR
*
         XDECI 4,$ICSTID      READING CUSTOMER ID
         ST    4,$CUSTID      STORING CUSTOMER ID
         MVC   $CUSTNM(20),$ICUSTNM    MOVING NAME OF CUSTOMER
         MVC   $SHPADDR(30),$ISHPADR   MOVING SHIPPING ADDR FOR CUST
         XDECI 4,$IRGSLTX     READ VALUE OF REGIONAL SALES TAX
         ST    4,$RGSLSTX     STORE VALUE OF REGIONAL SALES TAX
         LA    3,60(,3)       LOAD NEXT ROW IN TABLE
         XREAD IBUFFER,80     READ NEXT
         B     CUSREAD        BRANCH TO TOP OF LOOP
*
******************************************************************
*
* READ PROCESS FOR CUSTOMER DATA TABLE ENDS
*
* READ PROCESS FOR ORDER DATA TALE BEGINS
*
******************************************************************
*
ENDCUS   DS    0H
         LA    6,IBUFFER      LOADING INPUT BUFFER INTO R6
         LA    7,ORDTBL       LOADING ORDER TABLE INTO R7
         XREAD IBUFFER,80     PRIMING READ
*
******************************************************************
*
* READ LOOP FOR ORDER DATA TABLE BEGINS
*
******************************************************************
*
ORDREAD  DS    0H
         CLI   IBUFFER,C'*'   COMPARE IBUFFER WITH IMMEDIATE CHAR '*'
         BE    ENDORD         BRANCH OUT IF INPUT REACH '*' CHAR
*
         XDECI 4,$ICUST       READING VALUE OF CUSTOMER ID
         ST    4,$CSTID       STORING VALUE OF CUSTOMER ID
         XDECI 4,$IORDID      READING VALUE OF ORDER ID
         ST    4,$ORDID       STORING VALUE OF ORDER ID
         XDECI 4,$IMONTH      READING VALUE OF MONTH
         ST    4,$MONTH       STORING VALUE OF MONTH
         XDECI 4,$IDAY        READING VALUE OF DAY
         ST    4,$DAY         STORING VALUE OF DAY
         XDECI 4,$ISUBTOT     READING VALUE OF SUBTOTAL
         ST    4,$SUBTOTL     STORING VALUE OF SUBTOTAL
         XDECI 4,$ISHIPP      READING VALUE OF SHIPPING COST
         ST    4,$SHPPNG      STORING VALUE OF SHIPPING COST
         MVC   $PAYMETH(10),$IPAY      MOVING PAYMENT METHOD OF ORDER
         MVC   $STATUS(6),$ISTAT       MOVING CHARS FOR STATUS OF ORDER
         LA    7,40(,7)       LOAD THE NEXT ROW OF ORDER TABLE
         XREAD IBUFFER,80     READ NEXT
         B     ORDREAD        BRANCH TO TOP OF LOOP
*
******************************************************************
*
* READ PROCESS FOR ORDER DATA TABLE ENDS
*
* READ PROCESS FOR FISCAL PREDICTION DATA TALE BEGINS
*
******************************************************************
*
ENDORD   DS    0H
         LA    9,IBUFFER      LOAD THE INPUT BUFFER INTO R9
         LA    10,FISCTBL     LOAD FISCAL PREDICTION TABLE INTO R10
         XREAD IBUFFER,80     PRIMING READ
*
******************************************************************
*
* READ LOOP FOR FISCAL PREDICTION DATA TABLE BEGINS
*
******************************************************************
*
FSCREAD  DS    0H
         BNZ   ENDREAD        BRANCH OUT IF NOTHING LEFT TO READ
*
         XDECI 4,$IMNTH       READING MONTH
         ST    4,$MNTH        STORING MONTH
         XDECI 4,$IPREDCT     READING PREDICTED TOTAL OF MONTH
         ST    4,$PREDCT      STORING PREDICTED TOTAL OF MONTH
         XDECI 4,$ILOWER      READING VALUE OF LOWER BOUND
         ST    4,$LOWER       STORING VALUE OF LOWER BOUND
         XDECI 4,$IUPPER      READING VALUE OF UPPER BOUND
         ST    4,$UPPER       STORING VALUE OF UPPER BOUND
         LA    10,16(,10)     LOADING IN NEXT ROW IN TABLE
         XREAD IBUFFER,80     READ NEXT
         B     FSCREAD        BRANCH TO TOP OF LOOP
*
******************************************************************
*
* READ PROCESS FOR ORDER DATA TABLE ENDS
*
* DUMP TABLES
*
******************************************************************
*
ENDREAD  DS    0H
         XDUMP CUSTTBL,60*10
         XDUMP ORDTBL,40*30
         XDUMP FISCTBL,12*16
*
******************************************************************
*
* EXIT LINKAGE
*
******************************************************************
*
PRGEXIT  LA    15,0           R15 = RETURN CODE OF 0
         L     13,4(,13)      POINT R13 TO CALLER'S SAVE AREA
         L     14,12(,13)     RESTORE REGISTER 14
         LM    0,12,20(13)    RESTORE R0 THRU R12
         BR    14
*
******************************************************************
*
* LTORG BEGIN MARKER
*
******************************************************************
*
         LTORG
*
MAINSAVE DC    18F'-1'
IBUFFER  DS    CL80           INPUT BUFFER
         DC    C'*'
OBUFFER  DC    C' '           USE SINGLE SPACING
         DC    CL132' '       EMPTY SPACE OUTPUT BUFFER
*
         DS    0F
CUSTTBL  DS    10CL60         CUSTOMER INFORMATION TABLE
*
         DS    0F
ORDTBL   DS    30CL40         ORDER INFORMATION TABLE
*
         DS    0F
FISCTBL  DS    12CL16         FICAL PREDICTIOM TABLE
*
         END   MAIN
/*
//*
//* IN-STREAM PROGRAM DATA
//FT05F001 DD *
15402912 Marriana Trench      1792 Arriaga Dr. 80231         1025
44545886 Harvey Blanchard     279 Brace Blvd. 80244          0975
17619857 Juan Inviego         9411 Sunny Ct. 81657           1225
18851608 Francesca Donialo    2751 W. Rte 138 80244          0975
34280166 Wynona Brady         1966 Red Cliff Pl. 80538       0890
95841256 Courtney Savatino    21248 E Nassau Dr. 80013       1150
34292507 Emilio Estivolo      124 Willow Bridge Rd. 81657    1225
94282235 Tilly Carlson        5530 N Saint Louis Dr. 80538   0890
38291736 Ginger Kapinski      315 Araphoe Ave. 80302         0725
94136952 Michelle Martin      124 Meadow Rd. 81137           0915
*
15402912 14756301 01 25 466469 1842 CHECK      OPEN
44545886 51532621 02 08 551943 4743 MASTERCARD CLOSED
34280166 18859136 01 03 437429 2668 MASTERCARD CLOSED
15402912 41697723 04 28 768587 7300 MASTERCARD OPEN
44545886 22012867 02 25 384311 9475 MONEY ORD  CLOSED
17619857 46556099 03 18 110371 7835 MONEY ORD  CLOSED
44545886 05501637 04 11 266786 9121 MONEY ORD  OPEN
15402912 99159294 05 10 092900 1462 MASTERCARD CLOSED
17619857 95859743 03 01 591679 5582 MONEY ORD  OPEN
17619857 53252710 03 05 113520 5808 VISA       CLOSED
15402912 31771765 02 21 214423 6180 CHECK      OPEN
94282235 63288674 02 11 863685 3699 VISA       CLOSED
34280166 39960328 01 22 598187 2341 CHECK      OPEN
17619857 81886807 04 18 324470 2940 CHECK      CLOSED
17619857 81263710 05 12 232982 2488 MONEY ORD  CLOSED
38291736 93491046 07 08 275767 4793 CHECK      OPEN
94136952 53855284 06 17 794285 4829 VISA       CLOSED
44545886 87803805 06 01 206797 7445 CHECK      CLOSED
94282235 08957740 07 21 210540 3830 VISA       CLOSED
38291736 21555437 05 10 107113 1400 VISA       CLOSED
18851608 92977169 07 01 319863 3569 VISA       CLOSED
38291736 67387323 07 29 027243 1518 CHECK      CLOSED
18851608 10249511 08 10 926274 2167 MONEY ORD  OPEN
18851608 40460790 06 04 492617 4415 MONEY ORD  CLOSED
94282235 58708217 04 26 523011 5681 MONEY ORD  OPEN
94136952 61963908 02 21 555563 6093 CHECK      OPEN
38291736 04046134 08 17 920010 1067 CHECK      CLOSED
34280166 54687813 05 20 062958 5811 VISA       CLOSED
94136952 04086527 01 05 837673 4189 MASTERCARD CLOSED
94282235 73796093 03 10 076761 3684 CHECK      OPEN
*
01 021264992 021189952 021340032
02 021755304 021687474 021823134
03 024424666 024401712 024447620
04 025297203 025043531 025550875
05 017563231 017388609 017737853
06 023575859 023569004 023582714
07 020480888 020468491 020493284
08 021320946 021163889 021478004
09 019890210 019889988 019890433
10 024105357 024073928 024136785
11 020241102 020120967 020361238
12 027001066 026772081 027230051
/*
//
